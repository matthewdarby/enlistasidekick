apply plugin: 'war'
apply plugin: 'eclipse-wtp'
apply plugin: 'org.akhikhl.gretty'

// JDK 7
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
   mavenLocal()
   mavenCentral()
}

dependencies {
   providedCompile 'javax.servlet:servlet-api:2.5'
   compile 'ch.qos.logback:logback-classic:1.1.3'   
   compile 'org.springframework:spring-webmvc:4.2.1.RELEASE'
   compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.12'
   runtime 'javax.servlet:jstl:1.2'
}

//Gretty Embedded Jetty
buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath 'org.akhikhl.gretty:gretty:+'
  }
}

// Don't use Jetty8, even it's a servlet 3.0+ container, 
// but not support non-jar WebApplicationInitializer scanning.
// It will cause "No Spring WebApplicationInitializer types detected on classpath"
gretty {
  port = 9000
  contextPath = '/'
  servletContainer = 'jetty9' //tomcat7 or tomcat8
  // servletContainer = 'tomcat7'  
}

//For Eclipse IDE only
eclipse {

  wtp {
    component {
      
      //define context path, default to project folder name
      contextPath = '/'
      
    }
    
  }
} 